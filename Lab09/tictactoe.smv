-------------------------------------------------------------------------------
-- nuXmv model implementing the "tris" (tic-tac-toe) puzzle
--
--               X |   |
--             ____|___|____
--                 | X |
--             ____|___|____
--               O |   | O
--                 |   |
--
-- Numbering of the board squares:
--
--               1 | 2 | 3
--             ____|___|____
--               4 | 5 | 6
--             ____|___|____
--               7 | 8 | 9
--                 |   |
-------------------------------------------------------------------------------

MODULE main

-- the board
-- "0" means empty,
-- "1" filled by player 1,
-- "2" filled by player 2
VAR
  B : array 1..9 of {0,1,2};


-- initially, all squares are empty
INIT
  B[1] = 0 &
  B[2] = 0 &
  B[3] = 0 &
  B[4] = 0 &
  B[5] = 0 &
  B[6] = 0 &
  B[7] = 0 &
  B[8] = 0 &
  B[9] = 0;


-- let us assume that player 1 is the first player
-- players move alternatively
VAR
  player : 1..2;
ASSIGN
  init(player) := 1;
  next(player) := case
    player = 1 : 2;
    player = 2 : 1;
  esac;


-- moves
-- move=0 means no move
-- move=i with i>0 means the current player fills B[i]
VAR
  move : 0..9;
INIT
  move=0
TRANS
  next(move) = 0 ->
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 1 ->
	next(B[1]) = player &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 2->
	next(B[2]) = player &
	next(B[1])=B[1] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 3->
	 next(B[3]) = player &
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 4->
	next(B[4]) = player &
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 5->
	 next(B[5]) = player &
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 6 ->
	next(B[6]) = player &
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[7])=B[7] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 7 ->
	next(B[7]) = player &
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[8])=B[8] &
	next(B[9])=B[9]

TRANS
  next(move) = 8 ->
	 next(B[8]) = player &
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[9])=B[9]

TRANS
  next(move) = 9 ->
	next(B[9]) = player &
	next(B[1])=B[1] &
	next(B[2])=B[2] &
	next(B[3])=B[3] &
	next(B[4])=B[4] &
	next(B[5])=B[5] &
	next(B[6])=B[6] &
	next(B[7])=B[7] &
	next(B[8])=B[8]

TRANS
	B[9] != 0 -> next(move) != 9
TRANS
	B[8] != 0 -> next(move) != 8
TRANS
	B[7] != 0 -> next(move) != 7
TRANS
	B[6] != 0 -> next(move) != 6
TRANS
	B[5] != 0 -> next(move) != 5
TRANS
	B[4] != 0 -> next(move) != 4
TRANS
	B[3] != 0 -> next(move) != 3
TRANS
	B[2] != 0 -> next(move) != 2
TRANS
	B[1] != 0 -> next(move) != 1
	

-- "win1" means player 1 wins
-- "win2" means player 2 wins
-- "draw" means nobody wins
DEFINE
  win1 := (B[1]=1 & B[2]=1 & B[3]=1) |
          (B[4]=1 & B[5]=1 & B[6]=1) |
          (B[7]=1 & B[8]=1 & B[9]=1) |
          (B[1]=1 & B[4]=1 & B[7]=1) |
          (B[2]=1 & B[5]=1 & B[8]=1) |
          (B[3]=1 & B[6]=1 & B[9]=1) |
          (B[1]=1 & B[5]=1 & B[9]=1) |
          (B[3]=1 & B[5]=1 & B[7]=1);

  win2 := (B[1]=2 & B[2]=2 & B[3]=2) |
          (B[4]=2 & B[5]=2 & B[6]=2) |
          (B[7]=2 & B[8]=2 & B[9]=2) |
          (B[1]=2 & B[4]=2 & B[7]=2) |
          (B[2]=2 & B[5]=2 & B[8]=2) |
          (B[3]=2 & B[6]=2 & B[9]=2) |
          (B[1]=2 & B[5]=2 & B[9]=2) |
          (B[3]=2 & B[5]=2 & B[7]=2);

  draw := !win1 & !win2 &
          B[1]!=0 & B[2]!=0 & B[3]!=0 & B[4]!=0 &
          B[5]!=0 & B[6]!=0 & B[7]!=0 & B[8]!=0 & B[9]!=0;

TRANS
  (win1 | win2 | draw) <-> next(move)=0

LTLSPEC
  (! F draw)

LTLSPEC
 (! F win1)

LTLSPEC
 (! F win2)
